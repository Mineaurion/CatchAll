buildscript {
    repositories {
        mavenCentral()
        maven { url  = 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'com.github.johnrengelman.shadow'

    compileJava.options.encoding = 'UTF-8'
    group = project.pluginGroup
    version = project.pluginVersion

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    build.dependsOn shadowJar

    repositories {
        mavenCentral()
        maven { url = 'https://oss.sonatype.org/content/groups/public/' }
        maven {
            name = "GitHubPackages"
            url = uri('https://maven.pkg.github.com/Mineaurion/Api')
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }

    dependencies {
        api 'com.github.kevinsawicki:http-request:6.0'
        api 'com.google.code.gson:gson:2.8.9' // 2.9 version make sponge crash at startup
        compileOnly 'net.luckperms:api:5.4'
    }


    jar{
        archivesBaseName = project.jarName + "-" + project.name
    }

    shadowJar {
        minimize()
        dependencies {
            include(dependency(':common'))
            include(dependency('com.mineaurion.api:library:.*'))
            include(dependency('com.google.code.gson:gson'))
            include(dependency('com.github.kevinsawicki:http-request:.*'))
        }
        relocate 'com.google', 'com.mineaurion.catchall.lib.com.google'
        relocate 'com.github.kevinsawicki', 'com.mineaurion.catchall.lib.httprequest'
    }
}
